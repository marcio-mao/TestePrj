* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
DEFINE DATA
LOCAL
/*( imported data fields from Data Definition Module ADA-EMPL
1 VWAE VIEW OF ADA-EMPLOYEES
  2 PERSONNEL-ID (A8)
  2 FULL-NAME
    3 FIRST-NAME (A20)
    3 NAME (A20)
    3 MIDDLE-NAME (A20)
  2 MAR-STAT (A1)
  2 SEX (A1)
/*) imported data fields from Data Definition Module ADA-EMPL
1 CHAVES
  2 CH-NAME      (A20)
  2 CH-PERSONNEL (A8)
*
1 WS-ISN (P10)
1 WS-OPCAO(A1)
*
END-DEFINE
*
SET KEY PF2=PGM PF3=PGM PF4=PGM
*
*
INPUT (SG=OFF AD=MIL'_')
 '<< ADA-EMPLOYEES >>' (I)

     / CH-PERSONNEL
     / CH-NAME    
     // 'PF2'(I) 'CVMT' 3X 'PF3'(I) 'ENCERRA' /* 3X 'PF4'(I) 'CVMTBACA'
*
* IF *PF-KEY EQ 'PF2'
*    FETCH 'CVMTL001'
* END-IF
*
IF *PF-KEY EQ 'PF3'
   IF (*LEVEL EQ 1)
       STOP
   ELSE
       ESCAPE ROUTINE
   END-IF
END-IF
* *
* IF *PF-KEY EQ 'PF4'
*    FETCH 'CVMTBACA'
* END-IF
*
DECIDE FOR FIRST CONDITION
WHEN CH-PERSONNEL NE ' '
   FIND VWAE WITH PERSONNEL-ID      EQ CH-PERSONNEL
     IF NO RECORDS
        REINPUT 'NENHUM REGISTRO ENCONTRADO' ALARM
     END-NOREC
     MOVE *ISN TO WS-ISN
     PERFORM ATUALIZA-REGISTRO
     IF (*PF-KEY EQ 'PF3')
        ESCAPE ROUTINE
     END-IF
   END-FIND
WHEN CH-NAME NE ' '
   RD.
   FIND VWAE WITH NAME EQ CH-NAME 
     MOVE *ISN TO WS-ISN
     PERFORM ATUALIZA-REGISTRO
     IF (*PF-KEY EQ 'PF3')
        ESCAPE BOTTOM
     END-IF
   END-FIND
*   
   IF (*COUNTER(RD.) EQ 0)
      REINPUT 'NENHUM REGISTRO ENCONTRADO' ALARM
   END-IF
WHEN NONE
   IGNORE
END-DECIDE
*
FETCH 'BACAP'
*
* ----
DEFINE SUBROUTINE ATUALIZA-REGISTRO
* ----
*
GLP. GET VWAE WS-ISN 
*
INPUT (SG=OFF AD=MIL'_')
'<< ADA-EMPLOYEES - PF3 ENCERRA >>' (I)
 / '=' *ISN(AD=OI)
 / '=' PERSONNEL-ID
 / '=' FIRST-NAME 
 / '=' NAME
 / '=' MIDDLE-NAME
 / '=' MAR-STAT
 / '=' SEX
// 'I'(I) 'INCLUI' 'A'(I) 'ALTERA' 'E'(I) 'EXCLUI' 3X WS-OPCAO
*
  IF (*PF-KEY EQ 'PF3')
      BACKOUT TRANSACTION
      ESCAPE ROUTINE
  END-IF
*
  DECIDE ON FIRST VALUE OF WS-OPCAO
    VALUE 'I'
      STORE VWAE 
    VALUE 'E'
      DELETE(GLP.) 
    VALUE 'A'
      UPDATE(GLP.) 
    ANY VALUE
      END TRANSACTION
    NONE VALUE
      BACKOUT TRANSACTION
  END-DECIDE
*
  RESET WS-OPCAO
*
END-SUBROUTINE
*
END
